repositories {
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

configurations { generate }

ext {
	swagger_annotations_version = "1.5.0"
	gson_version = "2.6.1"
	httpclient_version = "4.3.+"
	junit_version = "4.8.1"

	supporting_files = [
			"ApiException.java",
			"ApiInvoker.java",
			"HttpPatch.java",
			"JsonUtil.java",
			"Pair.java"
	].join(",")
}

dependencies {
	compile "io.swagger:swagger-annotations:$swagger_annotations_version"
	compile "com.google.code.gson:gson:$gson_version"
	compile "io.swagger:swagger-annotations:$swagger_annotations_version"
	compile "org.apache.httpcomponents:httpcore:$httpclient_version"
	compile "org.apache.httpcomponents:httpclient:$httpclient_version"
	compile "org.apache.httpcomponents:httpmime:$httpclient_version"

	testCompile "junit:junit:$junit_version"

	// Dependency for the code generator only
	generate "io.swagger:swagger-codegen-cli:2.1.5"
}

task generateSwagger(type: JavaExec) {
	// Run generator
	classpath = configurations.generate
	main = "io.swagger.codegen.SwaggerCodegen"

	// Skip the build-related files
	jvmArgs = ["-Dmodels", "-Dapis", "-DsupportingFiles=$supporting_files"]// Configure for the API specification
	args = ["generate", "-i", "$schema_location", "-l", "android", "-o", "."]
}

task cleanGenerateSwagger(type: Delete) { delete "src/main/java/io/swagger/client" }
