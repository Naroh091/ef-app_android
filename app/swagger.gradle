repositories {
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

configurations { generate }

ext {
	swagger_annotations_version = "1.5.0"
	jackson_version = "2.4.2"
	jersey_version = "1.18"
	jodatime_version = "2.3"
	junit_version = "4.8.1"

	supporting_files = [
		"ApiClient.java",
		"ApiException.java",
		"Configuration.java",
		"Pair.java",
		"StringUtil.java",
		"ApiKeyAuth.java",
		"Authentication.java",
		"HttpBasicAuth.java",
		"OAuth.java",
		"OAuthFlow.java"
	].join(",")
}

dependencies {
	compile "io.swagger:swagger-annotations:$swagger_annotations_version"
	compile "com.sun.jersey:jersey-client:$jersey_version"
	compile "com.sun.jersey.contribs:jersey-multipart:$jersey_version"
	compile "com.fasterxml.jackson.core:jackson-core:$jackson_version"
	compile "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
	compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
	compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jackson_version"
	compile "com.brsanthu:migbase64:2.2"
	compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.1.5"
	compile "joda-time:joda-time:$jodatime_version"
	compile "com.michaelpardo:activeandroid:3.1.0-SNAPSHOT"
	compile "javax.annotation:javax.annotation-api:1.2"

	testCompile "junit:junit:$junit_version"

	// Dependency for the code generator only
	generate "io.swagger:swagger-codegen-cli:2.1.5"
}

task generateSwagger(type: JavaExec) {
	// Run generator
	classpath = configurations.generate
	main = "io.swagger.codegen.SwaggerCodegen"

	// Skip the build-related files
	jvmArgs = ["-Dmodels", "-Dapis", "-DsupportingFiles=$supporting_files"]// Configure for the API specification
	args = ["generate", "-i", "$schema_location", "-l", "java", "-o", ".", "-DserializableModel=true"]
}

task cleanGenerateSwagger(type: Delete) { delete "src/main/java/io/swagger/client" }
